rootProject.name = 'spring-xd'

include 'spring-xd-analytics'
include 'spring-xd-analytics-ml'
include 'spring-xd-analytics-ml-pmml'
include 'spring-xd-dirt'
include 'spring-xd-gemfire-server'
include 'redis'
include 'spring-xd-module'
include 'spring-xd-module-spi'
include 'spring-xd-tuple'
include 'spring-xd-hadoop'
include 'spring-xd-hadoop:hadoop12'
include 'spring-xd-hadoop:hadoop22'
include 'spring-xd-hadoop:cdh4'
include 'spring-xd-hadoop:hdp13'
include 'spring-xd-hadoop:hdp20'
include 'spring-xd-hadoop:phd1'
include 'spring-xd-yarn:spring-xd-yarn-client'
include 'spring-xd-yarn:spring-xd-yarn-appmaster'
include 'spring-xd-test'
include 'spring-xd-rest-client'
include 'spring-xd-rest-domain'
include 'spring-xd-shell'
include 'spring-xd-ui'
include 'modules'
include 'spring-xd-exec'
include 'spring-xd-integration-test'

['source', 'processor', 'sink', 'job'].each {kind->
	new File(rootDir, "modules/${kind}").eachDir { dir ->
		if (new File(dir, "config/${dir.name}.xml").exists()) {
		    include ":modules.${kind}.${dir.name}"
		    project(":modules.${kind}.${dir.name}").projectDir = dir
		}
	}
}

// For projects living in extensions/ directory:
// This prevents the detection of the extensions/ directory itself
// as a project (which it is not) and avoids manual listing of projects
new File(rootDir, 'extensions').eachDir { dir ->
	if (new File(dir, 'src/main/java').exists()) {
		include ":${dir.name}"
		project(":${dir.name}").projectDir = dir
	}
}

